#Turbo 20
#EVENT HanvarChainsOn		"#*#You are twisted by organic chains#*#"
#EVENT HanvarChainsOn		"#*#You resist the Chains of Anguish#*#"
#EVENT HanvarChainsOn		"#*#is entwined in chains#*#"
#EVENT HanvarChainsOn		"#*#entangles you in chains#*#"

#EVENT HanvarChainsOff		"#*#You break free of the chains#*#"

#EVENT HanvarStun       	"#*#Your eardrums begin to bleed#*#"
#EVENT HanvarStun			"#*#You resist the Wail of Anguish#*#"
#EVENT EngageHanvar			"#*#EngageHanvar#*#"

Sub Main(string _type, string _tankPoint)
	/melee aggro=on
    /melee taunt=on
	/declare bln_hanvarEngaged 		bool 	outer FALSE
	/declare bln_rootTank			bool	outer FALSE

	/declare timer_hanvar_stun 		timer 	outer 85
	/declare timer_hanvar_ae 		timer 	outer 180s
	/declare chainSeconds			int 	outer 0
	/declare stunSeconds			int   	outer 0	


	/declare bln_announcedRetreat   bool    outer FALSE
	/declare timer_announce_retreat	timer   outer 0
	/declare timer_announce_chains	timer  	outer 0
	/declare timer_announce_guards	timer  	outer 0
	/declare timer_announce_kill 	timer  	outer 0

	/echo Calling Hanvar Setup
	/dgt HanvarSetup

	:wait_loop
	/if (${Me.Buff[Chains of Anguish].ID}) {
		/varcalc chainSeconds (${Me.Buff[Chains of Anguish].Duration.TotalSeconds}) + 20
		/varset timer_hanvar_ae ${chainSeconds}s 
	}
	/if (!${timer_announce_chains}) /call announceHanvarAE
	/doevents
	/delay 5
	/if (!${bln_hanvarEngaged}) /goto :wait_loop

	| Announcing Engage
	/rs HanvarStart ${Time.Time24}


	:ActiveLoop
		/doevents
		
		| Announce AE every 5s
		/if (!${timer_announce_chains}) {
			/if (${Me.Buff[Chains of Anguish].ID}) {
				/varcalc chainSeconds (${Me.Buff[Chains of Anguish].Duration.TotalSeconds}) + 20
				/varset timer_hanvar_ae ${chainSeconds}s  
			} else {
				/varcalc chainSeconds (${timer_hanvar_ae.Value} / 10)
			}
			/call announceHanvarAE
		}

		|------
		| NPC/Hanvar Kills
		|------
		| Guards are up!
		/if (${SpawnCount[npc guard radius 150]}) {
			/if (!${timer_announce_guards}) /call commandAttackGuards
		} 
		
		| Run away!
		/if (${timer_hanvar_ae} <= 50 && !${bln_announcedRetreat}) {
			/if (!${timer_announce_retreat}) /call commandRetreat
		} 
		
		| Kill Hanvar
		/if (!${SpawnCount[npc guard radius 150]} && ${timer_hanvar_ae} > 50) {
			/if (!${timer_announce_kill}) /call commandAttackHanvar
		}

		/doevents
		/call getAggro
	/goto :ActiveLoop
/return

Sub announceHanvarAE
	/varcalc chainSeconds ${timer_hanvar_ae} / 10
	/cecho \at${timer_hanvar_ae} ${chainSeconds}s \awuntil Hanvar AE
	/rs HanvarAEonMT ${chainSeconds}
	/varset timer_announce_chains 5s
/return 


Sub commandAttackGuards
	/if (!${SpawnCount[npc guard radius 150]}) /return
	/cecho \ar${SpawnCount[npc guard radius 150]} \aware spawned, \ayKILLGUARDS
	/dex Xirin /target ${NearestSpawn[npc guard]}
	/dex Xirin /casting "Terror of Discord"
	/varset bln_announcedRetreat  FALSE
	/varset timer_announce_guards 10s
/return

Sub commandRetreat
	/cecho \ar${timer_hanvar_ae} ${chainSeconds}s \awuntil Hanvar AE, \aoRETREAT
	/rs HanvarRetreat ${chainSeconds}
	/varset bln_announcedRetreat TRUE
	/varset timer_announce_retreat 35s
/return 


Sub commandAttackHanvar
	/if (!${SpawnCount[npc guard]}) /rs HanvarKill ${chainSeconds}
	/cecho \ar${timer_hanvar_ae} ${chainSeconds}s \awuntil Hanvar AE  (no guards), \agKILLHANVAR
	/varset bln_announcedRetreat  FALSE
	/varset timer_announce_kill 10s
/return

| Hanvar fired his AE, safe to kill
Sub Event_HanvarChainsOn
	/varset timer_hanvar_ae 65s
	/call commandAttackHanvar
	/doevents flush HanvarChainsOn
/return

| Hanvar's chains wore off'
Sub Event_HanvarChainsOff
	/varset timer_hanvar_ae 20s
	/call commandAttackHanvar
	/doevents flush HanvarChainsOff
/return

Sub Event_EngageHanvar
	/if (!${Target.ID}) /target Hanvar
	/delay 10
	/if (${Me.CombatAbilityReady[Furious Discipline]}) /doability "Furious Discipline"
	/if (${Me.CombatAbilityReady[Ancient: Chaos Cry]}) /doability "Ancient: Chaos Cry"
	/delay 5
	/if (${Me.CombatAbilityReady[Bazu Bellow]}) /doability "Bazu Bellow"
	/nav wp hanvarTank
	
	:navHanvar
	/if (${Navigation.Active}) {
		/goto :navHanvar 
	}
	/varset bln_hanvarEngaged TRUE
	/face fast
	/attack on
/return

Sub Event_HanvarStun
	/varset timer_hanvar_stun 10s
	/echo we need to turn this timer into a number of seconds ${timer_hanvar_stun.Value}
	/varcalc stunSeconds ${timer_hanvar_stun.Value} / 10
	/rs HanvarStunonMT ${stunSeconds}
	/doevents flush HanvarStun
/return

Sub getAggro
	:aggroLoop
		/if (${Target.Distance} > 20 && ${bln_rootTank}) /warp f 15
		/if (${Me.TargetOfTarget.ID} != ${Me.ID}) {
			/echo Hanvar NOT TARGETTING ME
            /if (${Me.CombatAbilityReady["Bellow of the Mastruq"]}) /doability "Bellow of the Mastruq"
			/if (${Me.CombatAbilityReady["Bazu Bellow"]}) /doability "Bazu Bellow"
			/delay 10
			/goto :aggroLoop
		} 
/return

