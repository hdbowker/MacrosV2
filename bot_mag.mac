|**
----------------------------
BOT_MAG.mac
----------------------------
This macro is configured for use by any melee class
Modify the BOT_CLS_CHARNAME.ini file for use by a specific toon
This was written ground up for usage with xiris's bots.
It heavily relies on mq2melee for specific class functionality

Usage: /mac bot_mag ${_tank1Name} ${_tank2Name} ${_healPoint} ${_assistArgs}

______________________________________________
REVISION HISTORY
	11.16.10	xiris	INITIAL REVISION using modified bot_nec
	09.04.15	xiris	Changed the variant_initialize to better handle the output from the loader	
	12.04.15	xiris 	Further modifications to variant_initialize
	12.17.15	xiris 	Removed some extraneous /doevents calls
	07.05.16	xiris 	Brought inline with latest events/assist calls
	09.17.18    xiris   Included ALL xiris bot libraries in common instead of in individual macros
	04.05.22 	xiris	Added new Events aggregator and handlers
	04.16.22	xiris	Cleaned up some older code, normalized all vars/name
______________________________________________
REQUIRED PLUGINS
    mq2cast
    mq2eqbc
    mq2debuffs
    mq2moveutils
    
______________________________________________
REQUIRED INCLUDES  
	xiris_common/xiris_common.inc
**|


#include xiris_common/xiris_common.inc

#Turbo 40

#EVENT AEON 	"#*#aerampON"
#EVENT AEOFF	"#*#aerampOFF"

Sub InitEvents_Class
	/call RegisterClassEvents Events_MAG 2
/return

Sub Events_MAG
	/doevents AEON
	/doevents AEOFF
/return 

Sub Main(string _tank1Name, string _tank2Name, string _tank3Name, int _healPoint, string _assistArgs)
	/echo This line and the next here because calling a macro from a macro ...
	/echo ... apparently causes problems for the first line in the second macro
	/echo You are now in xiris_bot_${Me.Class.ShortName}.mac [I learned this from E3!~]

	|-- initialize the common xbot libraries
	/call xbot_initialize ${_tank1Name} ${_tank2Name} ${_tank3Name} ${_healPoint} ${_assistArgs}

	|-- initialize the macro specific libraries
	/call variant_initialize

	|-- report my status
	/bc [${Me.Class.ShortName}] ${Me.Name} ${str_action} >> ${Spawn[${_tank1Name}].CleanName} << 

	|-- target the MT
	/call TrueTarget ${int_MTID}

	|**
	----------------------------
	Main Loop
	----------------------------
	Runs in order listed,
	if adding any routine add the call here
	----------------------------
	**|

	:MainLoop
		
		/if (${Me.Casting.ID} && ${Me.Hovering}||${Window[tradewnd].Open}||${Window[lootwnd].Open}) /goto :MainLoop
		
		|-- Check to make sure MT is still alive, if not, find another
		/call chkMTAlive FALSE
		
		|-- Check to see if self needs to be cured (xiris_curing.inc)
		/call RunAllEvents
		/call chkNeedCure

		|-- Check Auto Assist if its on
		/if (${bln_autoAssist}) /call chkAutoAssist

		/if (${bln_engaged}) {
			|-- class specific
			/call RunAllEvents
			/call chkPetAttack
			/call chkDebuffs
			
			|-- dps library
			/call RunRaidEvents
    		/call RunClassEvents
			/call chkTarget
			/call chkQNuke
			/call chkNuke
			/call chkAENuke

			|-- std library
			/call RunRaidEvents
    		/call RunClassEvents
			/call chkAAs
			/call chkBurn
			/call chkClicks

		}

		|-- check utilities, includes: sit,loot,leash (out of combat only)
		/if (!${bln_engaged}) {
			/call chkUtils
			/call chkDownshits
		}

	|-- start again	
	/call chkMobDead
    /goto :MainLoop
/return


|**
 ----------------------------
 Target Checking
 ----------------------------
 Determines if the main assist has a new target, and whether we can attack
 Note: this is a variant of the sub in /xiris_common/xiris_common.inc
 ----------------------------
**|
Sub chkTarget
	|-- Make sure I have the target
	/if (!${Target.ID} || ((${Target.ID} !=  ${int_targetID}) && ${int_targetID} !=0) && (${SpawnCount[npc id ${int_targetID}]} > 0) && (${Spawn[id ${int_targetID}].PctHPs} != 0)) /call TrueTarget ${int_targetID}

	|-- Check for Target Death
	:chkTargetDead
    /if ((!${Target.ID} || ${Target.Type.NotEqual[NPC]}) && ${bln_engaged}) {
		/echo Mob is dead. Engage off.
        /varset bln_engaged FALSE
		/varset int_targetID 0
    } 
/return



|**
 ----------------------------
 Target Engage
 ----------------------------
 Engage the target, note usually triggered by EVENT_KILLMOB (see xiris_common.inc)
 Note: this is a variant of the sub in /xiris_common/xiris_common.inc
 ----------------------------
**|
Sub engageTarget(_targetID)
	/echo engageTarget ${Spawn[id ${_targetID}].CleanName} (${_targetID})
	/if (${_targetID} == 0) /return
	/varset int_targetID ${_targetID}
	/call TrueTarget ${int_targetID}
	/call Add2TargetArray ${_targetID}
	/pet attack
	/varset bln_engaged TRUE
/return


Sub targetReset
	/varset bln_engaged FALSE
	/varset bln_petAttacking FALSE
	/pet hold on
	/call CleanTargetArray
/return



|**
 ----------------------------
 Utility Checking
 ----------------------------
 Checks the utilities such as fail states (GM, Lobby, etc)
 Checks to see if we should med, loot, leash
 note: these routines are defined in xiris_common for the most part
 ----------------------------
**|
Sub chkUtils
	|-- Class Methods
	/call chkPet

	|-- Standard Methods
	/call chkSit
	/call chkLoot
	/call chkLeash
	
/return


Sub EVENT_AEON
	/echo AE Nuking ACTIVE
	/varset AENuke_Active TRUE
/return

SUB EVENT_AEOFF
	/echo AE Nuking DISABLED
	/varset AENuke_Active FALSE
/return


|**
 ----------------------------
 INITIALIZATION Subs
 ----------------------------
 Initialization of values for this specific macro variant
 ----------------------------
**| 

Sub variant_initialize

	|-- set up default vars
	/declare    str_action				string	outer	Assisting	
	
	/memspellset default
	/cecho \at${Me.Class.Name} Loaded: MT: ${str_MTName}(${int_MTID})HealPoint:${int_point_tankNormal} SAName: ${str_tankName2}(${int_STID})
	/call InitEvents_Class
/return













