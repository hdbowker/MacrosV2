|**
----------------------------
BOT_SHM.mac
----------------------------
This macro is configured for use by the SHAMAN class
Modify the BOT_CLS_CHARNAME.ini file for use by a specific toon
This was written ground up for usage with xiris's bots.

Usage: /mac bot_shm ${_tank1Name} ${_tank2Name} ${_healPoint} ${_assistArgs}

______________________________________________
REVISION HISTORY
	09.27.16	xiris	INITIAL REVISION
	10.24.16	xiris 	Moved Buffing to external include
	11.14.16	xiris 	Added support for an exclude list (see xiris_common for include)
	11.11.17	xiris   Added support for removal of mq2melee
	09.17.18    xiris   Included ALL xiris bot libraries in common instead of in individual macros
	12.22.21	xiris	Cleaned up some of the healing to bring inline with clerics
	01.02.22	xiris	Modified a bit to work a little smoother 
	04.05.22 	xiris	Added new Events aggregator and handlers
	04.16.22	xiris	Overhauled group heal method and variable names
						Moved commons healing functions to the include
______________________________________________
REQUIRED PLUGINS
    mq2cast
    mq2eqbc
    mq2debuffs
    mq2moveutils
    
______________________________________________
REQUIRED INCLUDES  
	xiris_common/xiris_common.inc
**|

#include xiris_common/xiris_common.inc

#turbo 50

#EVENT DOSHRINK "<#1#> doShrink#*#"
#EVENT DOSHRINK "[#1#(msg)] doShrink#*#"

Sub InitEvents_Class
	/call RegisterClassEvents Events_SHM 1
/return

Sub Events_SHM
	/doevents DOSHRINK
/return 

Sub Main(string _tank1Name, string _tank2Name, string _tank3Name, int _healPoint, string _assistArgs)
	
	
	/echo You are now in xiris_bot_${Me.Class.ShortName}.mac

	|-- initialize the common xbot libraries
	/call xbot_initialize ${_tank1Name} ${_tank2Name} ${_tank3Name} ${_healPoint} ${_assistArgs}

	|-- initialize the macro specific libraries
	/call variant_initialize

	|-- report my status
	/dgt [${Me.Class.ShortName}] ${Me.Name} ${str_action} >> ${Spawn[${_tank1Name}].CleanName} <<  @ ${int_point_tankPatch} ${_assistArgs}

	|-- target the MT
	/call TrueTarget ${int_MTID}

	|**
	| ----------------------------
	| Main Loop
	| ----------------------------
	| Runs in order listed,
	| if adding any routine add the call here
	| ----------------------------
	|**|
	
	:MainLoop
		/if (${Me.Hovering} || ${Window[tradewnd].Open} || ${Window[lootwnd].Open}) /goto :MainLoop
		|-- Check all Events
		/call RunAllEvents

		|-- Check to make sure MT is still alive, if not, find another
		/call chkMTAlive FALSE
		
		|-- Check to see if self needs to be cured (xiris_curing.inc)
		/call chkNeedCure

		|-- Check the three main groupings of heal classes (xiris_healing.inc)
		/call chkHeal_MT
		/call chkHeal_Self
		/call chkHeal_Group
		
		|-- Check for out of combat buffs (xiris_buffing.inc)
		/call RunAllEvents
		/call chkOOCBuffs
	
		|-- Check Auto Assist if its on
		/if (${bln_autoAssist}) /call chkAutoAssist
		
		/if (${bln_engaged}) {
			|-- check engaged events
			/call RunRaidEvents
			/call RunClassEvents

			|-- verify target status
			/call chkTarget	

			|-- debuff lib
			/call chkDebuffsOnSingleTarget ${int_targetID}
			
			|-- class specific
			/call chkPetAttack	
			/call chkChampion	
			/call cmbtBuffGRP	
			
			|-- dps library (caster)
			/call RunRaidEvents
    		/call RunClassEvents
			/call chkDOT
			/call chkNuke
			
			|-- std library (burns/aa/clicks)
			/call RunRaidEvents
    		/call RunClassEvents
			/call chkAAs
			/call chkBurn
			/call chkClicks
		} else {
			|-- check utilities, includes: sit,loot,leash (out of combat only)
			/call chkUtils
			/call chkDownshits
		}
	|-- start again	
    /goto :MainLoop
/return	




|**
 ----------------------------
 Cannibalization
 ----------------------------
 Applies Cannibalization if mana is below threshold
 ----------------------------
**|

Sub chkManaConvert
	/if (${Me.PctMana} < ${int_canniManaThreshold} && ${Me.SpellReady[${str_canniSpell}]} && ${Me.PctHPs} > ${int_canniHPThreshold}) {
		/echo "${str_canniSpell}"
		/call MQ2Cast "${str_canniSpell}" gem1 5s
	}
	
	/if (${Me.PctMana} < ${int_canniManaThreshold} && ${Me.AltAbilityReady[Cannibalization]} && ${Me.PctHPs} > ${int_canniHPThreshold}) {
		/echo AA "Cannibalization"
		/call MQ2Cast "Cannibalization" alt
	}
/return 

|**
----------------------------
Champion Checking
Champion is 1500 mana, so we don't want to cast it unless the timer is up
----------------------------  
**|

Sub chkChampion
	/if (!${bln_engaged}) 	/return
	/if (${timer_champion}) /return

	/call chkSpellMemorized "Champion" FALSE 7
	/if (!${Me.SpellReady[Champion]} && ${Me.SpellInCooldown[Champion]}) {
		| I must have just memmed champion, so uh, it takes 60s to be ready
		/varset timer_champion 60s
	} else {
		/call MQ2Cast "Champion"
		/varset timer_champion 510s
	}
/return



|**
 ----------------------------
 Utility Checking
 ----------------------------
 Checks the utilities such as fail states (GM, Lobby, etc)
 Checks to see if we should med, loot, leash
 note: these routines are defined in xiris_common for the most part
 ----------------------------
**|
Sub chkUtils
	|-- Shaman Specific
	/call chkManaConvert
	/call chkPet
	
	|-- Standard Methods
	/call chkSit
	/call chkLoot
	/call chkLeash
/return

Sub EVENT_DOSHRINK(_line, _chatSender, _eventParams)
	/echo \ay doShrinkEventCalled
	/if (${FindItemCount[Umbracite Swarm Orb]}) {
		/cecho \agShrinking
		/call MQ2Cast "Umbracite Swarm Orb" item
	}
/return


|**
 ----------------------------
 INITIALIZATION Subs
 ----------------------------
 Initialization of values for this specific macro variant
 ----------------------------
**|

Sub variant_initialize
	
	|-- set up default vars
	/declare    str_action				string	outer	Shamanizing	
	/memspellset default

	|-- Champion
	/declare 	champ_groups 				string  outer   ${Ini[botINI,Champion,champ_groups]}
	|/rs ${Me.Name} responsible for champion on ${champ_groups}

	/declare 	timer_champion				timer   outer   60s
	/declare 	timer_champion_1			timer   outer 	60s
	/declare 	timer_champion_2			timer   outer 	60s
	/declare 	timer_champion_3			timer   outer 	60s
	/declare 	timer_champion_4			timer   outer 	60s



	|-- Cannibalization
	/declare	int_canniManaThreshold	int		outer 	${Ini[botINI,Cannibalize,int_canni_mana_threshold,80]}
	/declare	int_canniHPThreshold	int		outer 	${Ini[botINI,Cannibalize,int_canni_hp_threshold,40]}
	/declare	str_canniSpell			string	outer 	${Ini[botINI,Cannibalize,str_canni_spell,Ancient:Chaotic Pain]}
	
	/cecho \at${Me.Class.Name} Loaded: MT: ${str_MTName}(${int_MTID})HealPoint:${int_point_tankPatch} ST: ${str_tankName2}(${int_STID})
	/call InitEvents_Class
/return

